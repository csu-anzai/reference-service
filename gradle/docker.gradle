apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.5'
    }
}

docker {
    registryCredentials {
        url = 'alvch-dockerv2-local.jfrog.io'
        username = System.env.ARTIFACTORY_USERNAME
        password = System.env.ARTIFACTORY_PASSWORD
    }
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

task copyDockerFiles(type: Copy) {
    from project.file("Dockerfile")
    from("${project.buildDir}/libs") {
        rename { fileName ->
            fileName.replace("-${project.version}", "")
        }
    }
    into "${project.buildDir}/docker"
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn copyDockerFiles
    inputDir = project.file("${project.buildDir}/docker")
    tags = [
        "alvch-dockerv2-local.jfrog.io/${project.name}:latest".toString(),
        "alvch-dockerv2-local.jfrog.io/${project.name}:${project.version}".toString()
    ]
}

task pushDockerImage(type: DockerPushImage) {
    dependsOn buildDockerImage
    imageName = "alvch-dockerv2-local.jfrog.io/${project.name}"
    tag = "${project.version}"
}
