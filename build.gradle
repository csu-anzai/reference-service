buildscript {
    repositories {
        jcenter()
        maven { url "http://repo.spring.io/plugins-release" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://dl.bintray.com/jhipster/maven" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}"
        classpath "io.spring.gradle:propdeps-plugin:0.0.10.RELEASE"
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17"
        //jhipster-needle-gradle-buildscript-dependency - JHipster will add additional gradle build script plugins here
    }
}

plugins {
    id 'io.github.http-builder-ng.http-plugin' version '0.1.1'
    id 'org.owasp.dependencycheck' version '3.3.2'
    id "net.ltgt.apt-idea" version "0.13"
    id "io.spring.dependency-management" version "1.0.4.RELEASE"
    id 'com.avast.gradle.docker-compose' version '0.7.1'
    //jhipster-needle-gradle-plugins - JHipster will add additional gradle plugins here
}

apply from: "gradle/owasp-dep-check.gradle"

allprojects {
    group 'ch.admin.seco.service.reference'
    version = version != 'unspecified' ? version : '1.0.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'idea'
    apply plugin: 'propdeps'
    apply plugin: 'maven'


    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations {
        compile.exclude module: 'spring-boot-starter-tomcat'
    }

    compileJava {
        options.compilerArgs << '-parameters'
    }

    repositories {
        maven { url 'https://repo.spring.io/release' }
        maven { url 'https://repo.spring.io/milestone' }
        jcenter()
        maven { url 'https://alvch.jfrog.io/alvch/libs-releases' }
        maven { url 'https://dl.bintray.com/jhipster/maven' }
        maven { url 'http://repo.maven.apache.org/maven2' }
        maven { url 'https://mvnrepository.com/artifact' }
    }

    dependencyManagement {
        dependencies {
            dependency 'com.mattbertolini:liquibase-slf4j:2.0.0'
            dependency 'commons-io:commons-io:2.6'
            dependency "com.googlecode.libphonenumber:libphonenumber:${phonenumber_version}"
            dependency 'com.ryantenney.metrics:metrics-spring:3.1.3'
            dependency "io.github.jhipster:jhipster-framework:${jhipster_dependencies_version}"
            dependency 'io.jsonwebtoken:jjwt:0.9.0'
            dependency 'org.awaitility:awaitility:3.1.2'
            dependency 'com.google.guava:guava:27.1-jre'
            dependency "io.springfox:springfox-swagger2:${springfox_swagger2_version}"
            dependency "io.springfox:springfox-swagger-ui:${springfox_swagger2_version}"
            dependency 'javax.xml.bind:jaxb-api:2.3.0'
            dependency 'net.logstash.logback:logstash-logback-encoder:4.11'
            dependency 'org.zalando:problem-spring-web:0.22.1'
            dependency 'com.hazelcast:hazelcast-hibernate53:1.3.2'
            dependency "com.oracle:ojdbc7:${oracleVersion}"
        }
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${spring_boot_version}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${spring_cloud_version}"
        }
    }

    test {
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.3'
}
